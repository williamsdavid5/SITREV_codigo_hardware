#include <Wire.h>
#include <LiquidCrystal_I2C.h>
#include <TinyGPSPlus.h>
#include <HardwareSerial.h>
#include <SPI.h>
#include <SD.h>
#include <FS.h>
#include <WiFi.h>
#include <HTTPClient.h>
#include <WiFiClientSecure.h>
#include <ArduinoJson.h>
#include <MFRC522.h>

// === Cart√£o SD ===
#define SD_CS 33
// SPIClass para SD no HSPI
SPIClass spiSD(HSPI);

bool limiteCarregadoOffline = false; //controle do limite carregado offline
//para controlar quando o gps nao funciona e precisamos de um limite offline
//ela evita que o sistema tente carregar repetidamente o limite do cart√£o
StaticJsonDocument<256> motoristaAtual;
bool motoristaEncontrado = false;

// === GPS ===
#define RX_GPS 16
#define TX_GPS -1
HardwareSerial gpsSerial(2);  // UART2
TinyGPSPlus gps;

bool gpsAtivo = false;

// === LCD ===
LiquidCrystal_I2C lcd(0x27, 16, 2);
boolean lcdFlag = false; //flag para controlar a impress√£o no lcd
//ajuda a impedir impress√£o constante desnecess√°ria

// === Wi-Fi e API ===
const char* ssid = "Duarte_Fotos";
const char* password = "05519558213";
const char* apiURL = "https://telemetria-fvv4.onrender.com/cercas";
const char* apiMotoristas = "https://telemetria-fvv4.onrender.com/motoristas/limpo";

// === Atualiza√ß√£o peri√≥dica ===
// unsigned long ultimaAtualizacao = 0;
// const unsigned long intervaloAtualizacao = 5 * 60 * 1000; // 5 minutos
unsigned long ultimaVerificacaoCercas = 0;
unsigned long intervaloVerificacaoCercas = 15000; // 5 segundos

int vel_max;
int vel_max_chuva;

float vel = 0;
unsigned long ultimaImpressaoVel = 0;

#define potenciometro 34
#define vel_max_potenciometro 80

//============= paraa l√≥gica de tolerancia de 10 segundos
bool alertaVelocidade = false;
unsigned long inicioAlerta = 0;
const unsigned long TOLERANCIA_ALERTA = 10000; // 10 segundos
const unsigned long INTERVALO_NORMAL = 15000; // 15 segundos padr√£o
const unsigned long INTERVALO_ALERTA = 3000;  // 3 segundos durante alerta
bool chuva = false; // sensor IR

// === RFID ===
#define RST_PIN 27
#define SS_PIN_RFID 15

MFRC522 mfrc522(SS_PIN_RFID, RST_PIN);
SPIClass spiRFID(VSPI);  // VSPI para RFID

boolean rfidLido = false;
String rfidValor;
String ultimoUID = "";
unsigned long viagemId = 0;

#define LED_PIN 2  // LED embutido do ESP32
#define BUZZER_PIN 17

//variaveis auxiliares para a logica de registrar viajem offline
//para que os dados sejam armazenados no cart√£o
File arquivoViagem;
String nomeArquivoViagem = "";
bool viagemAtiva = false;
// bool primeiroRegistro = true;

// === ID do Ve√≠culo ===
const int VEICULO_ID = 2;

// vair√°veis para a l√≥gica de registro e recupera√ß√£o de viajens n√£o encerradas
const String ARQUIVO_VIAGEM_ATUAL = "/viagem_atual.tmp";
bool viagemRecuperada = false;

//para as fun√ß√µes de escrita e leitura de registros
char jsonBuffer[256];

//prototipo das fun√ß√µes
void verificarMotoristaPorRFID();
void salvarUltimoLimite();
void encerrarViagem(float destinoLat, float destinoLng);

unsigned long ultimaLeituraRFID = 0;
const unsigned long DEBOUNCE_RFID = 1000;

// para a logica de atualiza√ß√£o assincrona ------------------------------
TaskHandle_t taskAtualizacaoHandle = NULL;
bool atualizacaoEmAndamento = false;
unsigned long proximaAtualizacao = 0;
const unsigned long INTERVALO_ATUALIZACAO = 5 * 60 * 1000; // 5 minutos
//prototipos
void taskAtualizacao(void* parameter);
void iniciarAtualizacaoAssincrona();

// para a logica de envio a API -----------------------------------------
TaskHandle_t taskEnvioViagensHandle = NULL;
bool envioViagensAtivo = false;
// const unsigned long INTERVALO_ENVIO_VIAGENS = 30000; // 30 segundos
const char* apiRegistrarViagem = "https://telemetria-fvv4.onrender.com/viagens/registrar-viagem";

// Prot√≥tipos
void taskEnvioViagens(void* parameter);
void iniciarEnvioViagens();
bool enviarViagemParaAPI(const String& caminhoArquivo);


void processarCartao(String uid) {
  if (millis() - ultimaLeituraRFID < DEBOUNCE_RFID) {
    Serial.println("‚è∞ Debounce RFID: leitura ignorada (muito r√°pida)");
    return;
  }
  ultimaLeituraRFID = millis();

  // Guarda o UID atual para verifica√ß√£o
  String uidAnterior = rfidValor;
  rfidValor = uid;
  
  // Verifica se √© um motorista autorizado
  verificarMotoristaPorRFID();
  
  if (!motoristaEncontrado) {
    Serial.println("‚ùå Cart√£o n√£o autorizado");
    rfidValor = uidAnterior; // Restaura o UID anterior
    return;
  }

  if (!rfidLido) {
    // In√≠cio da viagem
    rfidLido = true;
    ultimoUID = uid;
    lcdFlag = true;
    Serial.println("üöó Viagem iniciada");
  } else {
    // Se j√° h√° viagem em andamento
    if (uid == ultimoUID) {
      // Mesmo motorista - encerra viagem
      rfidLido = false;
      limiteCarregadoOffline = false;
      salvarUltimoLimite();
      motoristaEncontrado = false;
      ultimoUID = "";
      // viagemId = 0;
      lcd.clear();
      lcd.print("Viagem encerrada");
      Serial.println("üõë Viagem encerrada");

      encerrarViagem();

      delay(3000);
      lcd.clear();
      lcd.print("Inicie a viagem");
    } else {
      // Motorista diferente - encerra viagem atual e inicia nova
      Serial.println("üîÑ Motorista diferente - trocando viagem");
      
      // Primeiro encerra a viagem atual
      encerrarViagem();
      
      // Limpa vari√°veis da viagem anterior
      rfidLido = false;
      limiteCarregadoOffline = false;
      salvarUltimoLimite();
      ultimoUID = "";
      
      // Inicia nova viagem com o novo motorista
      rfidLido = true;
      ultimoUID = uid;
      lcdFlag = true;
      
      Serial.println("üöó Nova viagem iniciada com outro motorista");
      
      // Feedback visual
      lcd.clear();
      lcd.print("Troca motorista");
      lcd.setCursor(0, 1);
      lcd.print(motoristaAtual["nome"].as<const char*>());
      
      delay(2000);
    }
  }
}

//------------------------------------------------------------------------

void taskRFID(void* parameter) {
  Serial.println("RFID pronto");
  bool cartaoPresente = false;

  unsigned long ultimaLeitura = 0;
  static unsigned long ultimaVerificacao = millis();
  const unsigned long intervaloAntiRepeticao = 1000; // 1 segundo entre leituras

  for (;;) {
    if (mfrc522.PICC_IsNewCardPresent() && mfrc522.PICC_ReadCardSerial()) {
      String uid = "";
      for (byte i = 0; i < mfrc522.uid.size; i++) {
        if (mfrc522.uid.uidByte[i] < 0x10) uid += "0";
        uid += String(mfrc522.uid.uidByte[i], HEX);
      }
      uid.toUpperCase();

      // Preven√ß√£o contra leituras repetidas muito r√°pidas
      if (millis() - ultimaLeitura > intervaloAntiRepeticao) {
        ultimaLeitura = millis();

        if (!cartaoPresente) {
          // Apenas processa quando o cart√£o for inserido novamente
          digitalWrite(LED_PIN, HIGH);
          digitalWrite(BUZZER_PIN, HIGH);
          delay(50);
          digitalWrite(BUZZER_PIN, LOW);
          delay(35);
          digitalWrite(BUZZER_PIN, HIGH);
          delay(50);
          digitalWrite(BUZZER_PIN, LOW);

          Serial.print("üîç UID lido: ");
          Serial.println(uid);

          rfidValor = uid;
          processarCartao(uid);
          cartaoPresente = true;
        }
      }

      mfrc522.PICC_HaltA();
    } else {
      if (cartaoPresente) {
        digitalWrite(LED_PIN, LOW);
        cartaoPresente = false; // Liberar para pr√≥xima leitura
        // Pequeno delay ap√≥s remover o cart√£o para evitar leitura fantasma
        vTaskDelay(300 / portTICK_PERIOD_MS);
      }
    }

    vTaskDelay(50 / portTICK_PERIOD_MS);
  }
}

float lerVelocidadePotenciometro() {
  int leitura = analogRead(potenciometro);
  // Converte leitura ADC (0-4095) para velocidade (0-80 km/h)
  return map(leitura, 0, 4095, 0, vel_max_potenciometro);
}

//------------------------------------------------------------------------

void setup() {
  Serial.begin(115200);

  pinMode(LED_PIN, OUTPUT);
  pinMode(BUZZER_PIN, OUTPUT);

  digitalWrite(LED_PIN, LOW);
  digitalWrite(BUZZER_PIN, LOW);

  pinMode(potenciometro, INPUT);

  limiteCarregadoOffline = false;

  pinMode(RST_PIN, OUTPUT);
  digitalWrite(RST_PIN, HIGH);
  delay(3000);

  gpsSerial.begin(9600, SERIAL_8N1, RX_GPS, TX_GPS);

  unsigned long inicioGPS = millis();
  bool gpsRespondendo = false;
  while (millis() - inicioGPS < 3000) { // 3 segundos de teste
      while (gpsSerial.available()) {
          char c = gpsSerial.read();
          Serial.write(c); // imprime cada caractere vindo do GPS
          gpsRespondendo = true;
      }
  }

  if (gpsRespondendo) {
      Serial.println("\n‚úÖ GPS respondendo!");
  } else {
      Serial.println("\n‚ö†Ô∏è GPS n√£o respondeu, verifique conex√µes.");
  }

  Wire.begin(4, 5);
  lcd.begin(16, 2);
  lcd.backlight();
  lcd.clear();
  lcd.print("Iniciando");
  lcd.setCursor(0, 1);
  lcd.print("Aguarde...");

  // Iniciar SPI do rfid
  digitalWrite(RST_PIN, LOW);
  delay(200);
  digitalWrite(RST_PIN, HIGH);
  delay(200);
  // delay(3000);

  SPI.begin(14, 12, 13, SS_PIN_RFID);
  // spiRFID.begin(14, 12, 13, SS_PIN_RFID);
  mfrc522.PCD_Init();
  delay(1000);
  Serial.print("RFID Version: ");
  byte v = mfrc522.PCD_ReadRegister(mfrc522.VersionReg);
  Serial.println(v, HEX);

  // Serial.println("Conectando ao Wi-Fi...");
  WiFi.begin(ssid, password);
  unsigned long inicioWifi = millis();

  // Inicia SD no HSPI (pinos 25,26,21,33)
  spiSD.begin(25, 26, 21, SD_CS);
  if (!SD.begin(SD_CS, spiSD)) {
    Serial.println("‚ùå Falha ao iniciar cart√£o SD");
    lcd.begin(16, 2);
    lcd.backlight();
    lcd.clear();
    lcd.print("Falha no SD!");
    while(true) delay(1000); // trava se falhar no SD
  }
  Serial.println("‚úÖ Cart√£o SD iniciado");

  if (!SD.exists("/viagens")) {
    SD.mkdir("/viagens");
    Serial.println("üìÅ Diret√≥rio /viagens criado");
  }

  if (!SD.exists("/pendente")) {
    SD.mkdir("/pendente");
    Serial.println("üìÅ Diret√≥rio /pendente criado");
  }

  // Verificar se h√° viagens pendentes para recuperar
  File pendenteDir = SD.open("/pendente");
  if (pendenteDir && pendenteDir.isDirectory()) {
      bool temArquivos = false;
      while (true) {
          File entry = pendenteDir.openNextFile();
          if (!entry) break;
          if (!entry.isDirectory() && String(entry.name()).endsWith(".json")) {
              temArquivos = true;
              entry.close();
              break;
          }
          entry.close();
      }
      pendenteDir.close();
      
      if (temArquivos) {
          Serial.println("‚ö†Ô∏è Viagens pendentes encontradas. Recuperando...");
          recuperarViagemInterrompida();
      }
  }

  //para o rfid
  xTaskCreatePinnedToCore(
    taskRFID,     // Fun√ß√£o da tarefa
    "RFID Reader",// Nome da tarefa
    4096,         // Tamanho da stack
    NULL,         // Par√¢metro
    1,            // Prioridade
    NULL,         // Handle da tarefa
    0             // Core 0 (o loop() roda no Core 1)
  );

  //para a atualiza√ß√£o de cercas
  xTaskCreatePinnedToCore(
    taskAtualizacao,
    "Atualizacao",
    8192,
    NULL,
    1,
    &taskAtualizacaoHandle,
    0
  );
  //ja atualiza os dados no inicio do sistema
  proximaAtualizacao = millis() + 10000;

  xTaskCreatePinnedToCore(
      taskEnvioViagens,
      "EnvioViagens",
      4096,
      NULL,
      1,
      &taskEnvioViagensHandle,
      0
  );

  lcd.clear();
  lcd.setCursor(0, 0);
  lcd.print("Iniciando gps...");
}

//------------------------------------------------------------------------

void loop() {

  vel = lerVelocidadePotenciometro();

  if (millis() > proximaAtualizacao) {
    iniciarAtualizacaoAssincrona();
  }

  if (rfidLido) {

    if (rfidLido && lcdFlag) {
      delay(50);
      lcd.clear();
      lcd.setCursor(0, 0);
      lcd.print("Bem vindo(a)");
      lcd.setCursor(0, 1);
      lcd.print(motoristaAtual["nome"].as<const char*>());

      iniciarViagem();
      delay(3000);

      lcdFlag = false;
    }

    if (!gpsAtivo && !limiteCarregadoOffline) {
      if (carregarUltimoLimite()) {
        lcd.clear();
        lcd.setCursor(0, 1);
        lcd.print("Limite:");
        lcd.setCursor(8, 1);
        lcd.print(vel_max);
        lcd.print("km/h");

        Serial.println("‚úÖ Limite carregado do SD na aus√™ncia do GPS.");
        limiteCarregadoOffline = true;
      } else {
        Serial.println("‚ö†Ô∏è Falha ao carregar limite do SD.");
      }
    }

    int limiteAtual = chuva ? vel_max_chuva : vel_max;

    if (vel > limiteAtual && !alertaVelocidade) {
      alertaVelocidade = true;
      inicioAlerta = millis();
      digitalWrite(BUZZER_PIN, !digitalRead(BUZZER_PIN));
      Serial.println("üö® Velocidade acima do limite! Buzzer ativado.");
    }

    if (alertaVelocidade) {
      // Verifica se motorista reduziu dentro dos 10 segundos
      if (vel <= limiteAtual) {
        alertaVelocidade = false;
        digitalWrite(BUZZER_PIN, LOW);
        Serial.println("‚úÖ Velocidade normalizada. Alerta cancelado.");
      }
      else if (millis() - inicioAlerta > TOLERANCIA_ALERTA) {
        // Motorista n√£o reduziu, diminui intervalo de verifica√ß√£o
        intervaloVerificacaoCercas = INTERVALO_ALERTA;
        Serial.println("‚ö†Ô∏è Intervalo reduzido para 3s (alerta ativo)");
      }
    }
    else if (intervaloVerificacaoCercas != INTERVALO_NORMAL) {
      intervaloVerificacaoCercas = INTERVALO_NORMAL;
      Serial.println("‚Ü©Ô∏è Intervalo restaurado para 15s");
    }

    while (gpsSerial.available()) {
      gps.encode(gpsSerial.read());

      if (gps.location.isUpdated()) {
        gpsAtivo = true;

        float lat = gps.location.lat();
        float lng = gps.location.lng();
        // float vel = gps.speed.kmph(); // velocidade do obd2

        if (millis() - ultimaVerificacaoCercas > intervaloVerificacaoCercas) {

          if (!alertaVelocidade || (alertaVelocidade && millis() - inicioAlerta > TOLERANCIA_ALERTA)) {
            Serial.print("Lat: "); Serial.println(lat, 6);
            Serial.print("Lng: "); Serial.println(lng, 6);
            ultimaVerificacaoCercas = millis();

            verificarCercas(lat, lng);
            registrarPosicao(lat, lng, vel, chuva);
            iniciarEnvioViagens();
          }
        }
      }
    }

    if (millis() - ultimaImpressaoVel > 500) {
      lcd.setCursor(0, 0); 
      lcd.print("                ");
      lcd.setCursor(0, 0); 
      lcd.print(vel);
      lcd.setCursor(4, 0); 
      lcd.print("km/h");
      ultimaImpressaoVel = millis();
    }
    
  } else {

    if (!lcdFlag) {
      lcd.clear();
      lcd.setCursor(0, 0);
      lcd.print("Inicie a Viagem");
      lcdFlag = true;
    }
  }

  //salvamento automatico que independe do gps
  //o salvamento antigo s√≥ era feito ao registrar uma nova posi√ß√£o
  //agora √© feito independente do sinal do gps
  static unsigned long ultimoSave = 0;
  if (viagemAtiva && millis() - ultimoSave > 30000) {
    arquivoViagem.flush();
    ultimoSave = millis();
    Serial.println("üíæ Dados da viagem salvos (auto-salvamento)");
  }
}

// para o envio das viagens para a API
bool enviarViagemParaAPI(const String& caminhoArquivo) {
    File file = SD.open(caminhoArquivo, FILE_READ);
    if (!file) {
        Serial.println("‚ùå Erro ao abrir arquivo para envio");
        return false;
    }
    
    // L√™ todo o conte√∫do do arquivo
    String jsonData;
    while (file.available()) {
        jsonData += (char)file.read();
    }
    file.close();
    
    // üî• IMPORTANTE: Completa o JSON antes de enviar (se estiver incompleto)
    if (!jsonData.endsWith("]}")) {
        jsonData += "]}"; // Fecha o array de registros e o objeto principal
        Serial.println("üîß JSON completado com fechamento");
    }
    
    Serial.print("üìä Tamanho do JSON: ");
    Serial.print(jsonData.length());
    Serial.println(" bytes");
    
    WiFiClientSecure client;
    client.setInsecure();
    HTTPClient https;
    
    if (https.begin(client, apiRegistrarViagem)) {
        https.addHeader("Content-Type", "application/json");
        https.addHeader("User-Agent", "ESP32-Telemetria");
        
        Serial.println("üîÑ Enviando para API...");
        int httpCode = https.POST(jsonData);
        
        if (httpCode == 200 || httpCode == 201) {
            Serial.println("‚úÖ Viagem enviada com sucesso!");
            https.end();
            
            // ‚úÖ APENAS LOG - N√ÉO REMOVE O ARQUIVO!
            // O sistema principal j√° cuida disso atrav√©s da recuperarViagemInterrompida()
            Serial.println("üìã Arquivo mantido em /pendente para processamento normal");
            return true;
        } else {
            Serial.print("‚ùå Erro HTTP no envio: ");
            Serial.println(httpCode);
            Serial.print("Resposta: ");
            Serial.println(https.getString());
            https.end();
            return false;
        }
    } else {
        Serial.println("‚ùå Erro ao iniciar conex√£o HTTP");
        return false;
    }
}

void taskEnvioViagens(void* parameter) {
    Serial.println("üì§ Task de envio de viagens iniciada");
    
    for (;;) {
        if (envioViagensAtivo && WiFi.status() == WL_CONNECTED) {
            Serial.println("üîç Verificando viagens pendentes para envio...");
            
            File pendenteDir = SD.open("/pendente");
            if (pendenteDir && pendenteDir.isDirectory()) {
                bool enviouAlguma = false;
                
                while (true) {
                    File entry = pendenteDir.openNextFile();
                    if (!entry) break;
                    
                    String nomeArquivo = String(entry.name());
                    entry.close();
                    
                    // Ignora diret√≥rios e arquivos que n√£o s√£o .json
                    if (nomeArquivo.equals(".") || nomeArquivo.equals("..") || 
                        !nomeArquivo.endsWith(".json")) {
                        continue;
                    }
                    
                    String caminhoCompleto = "/pendente/" + nomeArquivo;
                    Serial.print("üì® Tentando enviar: ");
                    Serial.println(nomeArquivo);
                    
                    if (enviarViagemParaAPI(caminhoCompleto)) {
                        enviouAlguma = true;
                        Serial.println("‚úÖ Envio bem-sucedido");
                        // ‚úÖ N√ÉO REMOVE - o arquivo fica em /pendente para o processamento normal
                    } else {
                        Serial.println("‚ùå Falha no envio, ser√° tentado novamente depois");
                    }
                    
                    // Pequena pausa entre envios
                    vTaskDelay(1000 / portTICK_PERIOD_MS);
                }
                
                pendenteDir.close();
                
                if (!enviouAlguma) {
                    Serial.println("üì≠ Nenhuma viagem pendente para enviar");
                }
            } else {
                Serial.println("‚ùå Erro ao abrir pasta /pendente");
            }
            
            envioViagensAtivo = false;
        }
        
        vTaskDelay(5000 / portTICK_PERIOD_MS); // Verifica a cada 5 segundos
    }
}

void iniciarEnvioViagens() {
    if (WiFi.status() == WL_CONNECTED && !envioViagensAtivo) {
        envioViagensAtivo = true;
        Serial.println("üì§ Solicita√ß√£o de envio de viagens ativada");
    }
}

// l√≥gicas para motoristas e cercas ------------------------------------------------------------------------
// === Fun√ß√£o para atualiza√ß√£o ass√≠ncrona ===
void taskAtualizacao(void* parameter) {
  Serial.println("üîÑ Tarefa de atualiza√ß√£o iniciada");
  
  for (;;) {
    if (atualizacaoEmAndamento) {
      Serial.println("üîÑ Iniciando atualiza√ß√£o ass√≠ncrona de cercas e motoristas...");
      
      // Fazer ambas as requisi√ß√µes primeiro para aproveitar a janela de conex√£o
      WiFiClientSecure clientCercas;
      WiFiClientSecure clientMotoristas;
      HTTPClient httpsCercas;
      HTTPClient httpsMotoristas;
      
      bool cercasSucesso = false;
      bool motoristasSucesso = false;
      
      // REQUISI√á√ÉO DE CERCAS
      clientCercas.setInsecure();
      if (httpsCercas.begin(clientCercas, apiURL)) {
        httpsCercas.addHeader("User-Agent", "ESP8266");
        httpsCercas.addHeader("Accept", "application/json");
        httpsCercas.addHeader("Accept-Encoding", "identity");
        httpsCercas.setTimeout(10000);

        int httpCode = httpsCercas.GET();
        if (httpCode == 200) {
          Serial.println("‚úÖ Resposta OK das cercas. Preparando para salvar...");
          cercasSucesso = true;
        } else {
          Serial.print("‚ö†Ô∏è Falha na requisi√ß√£o de cercas. C√≥digo HTTP: ");
          Serial.println(httpCode);
          httpsCercas.end();
        }
      }

      // Pequeno delay entre as requisi√ß√µes
      vTaskDelay(300 / portTICK_PERIOD_MS);

      // REQUISI√á√ÉO DE MOTORISTAS
      clientMotoristas.setInsecure();
      if (httpsMotoristas.begin(clientMotoristas, apiMotoristas)) {
        httpsMotoristas.addHeader("User-Agent", "ESP8266");
        httpsMotoristas.addHeader("Accept", "application/json");
        httpsMotoristas.addHeader("Accept-Encoding", "identity");
        httpsMotoristas.setTimeout(10000);

        int httpCode = httpsMotoristas.GET();
        if (httpCode == 200) {
          Serial.println("‚úÖ Resposta OK dos motoristas. Preparando para salvar...");
          motoristasSucesso = true;
        } else {
          Serial.printf("Erro HTTP motoristas: %d\n", httpCode);
          httpsMotoristas.end();
        }
      }

      // AGORA PROCESSAR OS DADOS (se as requisi√ß√µes foram bem sucedidas)
      
      // PROCESSAR CERCAS
      if (cercasSucesso) {
        Serial.println("üíæ Salvando dados das cercas...");
        File temp = SD.open("/temp_cercas.json", FILE_WRITE);
        if (temp) {
          WiFiClient& stream = httpsCercas.getStream();

          unsigned long inicio = millis();
          const unsigned long tempoLimite = 10000;
          bool iniciouJson = false;
          bool transmissaoAtiva = true;

          while (transmissaoAtiva && (millis() - inicio) < tempoLimite) {
            if (stream.available()) {
              char c = stream.read();

              if (!iniciouJson) {
                if (c == '[' || c == '{') {
                  iniciouJson = true;
                  temp.write(c);
                }
              } else {
                temp.write(c);
              }

              inicio = millis();
            } else {
              vTaskDelay(100 / portTICK_PERIOD_MS);
              
              if (!stream.connected() && stream.available() == 0) {
                transmissaoAtiva = false;
                Serial.println("üì¶ Transmiss√£o de cercas conclu√≠da");
              }
            }
            
            vTaskDelay(10 / portTICK_PERIOD_MS);
          }

          temp.flush();
          temp.close();
          httpsCercas.end();
          Serial.println("‚úÖ Dados das cercas salvos temporariamente");

          if (validarEstruturaJSON("/temp_cercas.json")) {
            Serial.println("‚úÖ JSON de cercas v√°lido! Substituindo arquivo oficial...");
            if (SD.exists("/cercas.json")) SD.remove("/cercas.json");
            SD.rename("/temp_cercas.json", "/cercas.json");
            Serial.println("üìù Cercas atualizadas com sucesso.");
          } else {
            Serial.println("‚ö†Ô∏è JSON de cercas inv√°lido. Mantendo arquivo antigo.");
          }
        } else {
          Serial.println("‚ùå Erro ao abrir arquivo tempor√°rio para cercas.");
          httpsCercas.end();
        }
      }

      // PROCESSAR MOTORISTAS
      if (motoristasSucesso) {
        Serial.println("üíæ Salvando dados dos motoristas...");
        File temp = SD.open("/temp_motoristas.json", FILE_WRITE);
        if (temp) {
          WiFiClient& stream = httpsMotoristas.getStream();
          unsigned long inicio = millis();
          const unsigned long tempoLimite = 10000;
          bool iniciouJson = false;
          bool transmissaoAtiva = true;

          while (transmissaoAtiva && (millis() - inicio) < tempoLimite) {
            if (stream.available()) {
              char c = stream.read();
              if (!iniciouJson) {
                if (c == '[' || c == '{') {
                  iniciouJson = true;
                  temp.write(c);
                }
              } else {
                temp.write(c);
              }
              inicio = millis();
            } else {
              vTaskDelay(100 / portTICK_PERIOD_MS);
              
              if (!stream.connected() && stream.available() == 0) {
                transmissaoAtiva = false;
                Serial.println("üì¶ Transmiss√£o de motoristas conclu√≠da");
              }
            }
            
            vTaskDelay(10 / portTICK_PERIOD_MS);
          }

          temp.flush();
          temp.close();
          httpsMotoristas.end();
          Serial.println("‚úÖ Dados dos motoristas salvos temporariamente");

          if (validarEstruturaJSON("/temp_motoristas.json")) {
            Serial.println("üßæ JSON de motoristas v√°lido.");
            if (SD.exists("/motoristas.json")) SD.remove("/motoristas.json");
            SD.rename("/temp_motoristas.json", "/motoristas.json");
            Serial.println("üì¶ Motoristas atualizados com sucesso.");
          } else {
            Serial.println("‚ùå JSON de motoristas inv√°lido.");
          }
        } else {
          Serial.println("‚ùå Erro ao abrir temp_motoristas.json.");
          httpsMotoristas.end();
        }
      }

      Serial.println("‚úÖ Atualiza√ß√£o ass√≠ncrona conclu√≠da");
      atualizacaoEmAndamento = false;
      proximaAtualizacao = millis() + INTERVALO_ATUALIZACAO;
    }
    
    vTaskDelay(1000 / portTICK_PERIOD_MS);
  }
}

void iniciarAtualizacaoAssincrona() {
  if (WiFi.status() == WL_CONNECTED && !atualizacaoEmAndamento) {
    atualizacaoEmAndamento = true;
    Serial.println("üîÑ Solicitando atualiza√ß√£o ass√≠ncrona...");
  }
}

// void atualizarCercas() {
//   Serial.println("üîÑ Atualizando lista de cercas...");
//   WiFiClientSecure client;
//   client.setInsecure();
//   HTTPClient https;

//   if (https.begin(client, apiURL)) {
//     https.addHeader("User-Agent", "ESP8266");
//     https.addHeader("Accept", "application/json");
//     https.addHeader("Accept-Encoding", "identity");
//     https.setTimeout(10000); // 10 segundos de espera de resposta da API


//     int httpCode = https.GET();
//     if (httpCode == 200) {
//       Serial.println("Resposta OK. Salvando no arquivo tempor√°rio...");

//       // Salva em arquivo tempor√°rio
//       File temp = SD.open("/temp_cercas.json", FILE_WRITE);
//       if (temp) {
//         WiFiClient& stream = https.getStream();

//         unsigned long inicio = millis();
//         const unsigned long tempoLimite = 10000; // 10 segundos
//         bool iniciouJson = false;

//         while ((millis() - inicio) < tempoLimite) {
//           if (stream.available()) {
//             char c = stream.read();

//             if (!iniciouJson) {
//               if (c == '[' || c == '{') {
//                 iniciouJson = true;
//                 temp.write(c);
//               }
//             } else {
//               temp.write(c);
//             }

//             inicio = millis(); // Reinicia tempo sempre que l√™ algo
//           }
//         }

//         temp.flush();
//         temp.close();
//         Serial.println("‚úÖ Arquivo tempor√°rio salvo com sucesso!");

//         if (validarEstruturaJSON("/temp_cercas.json")) {
//           Serial.println("‚úÖ JSON parece v√°lido! Substituindo arquivo oficial...");

//           if (SD.exists("/cercas.json")) {
//             SD.remove("/cercas.json");
//           }

//           SD.rename("/temp_cercas.json", "/cercas.json");
//           Serial.println("üìù Substitui√ß√£o conclu√≠da.");
//         } else {
//           Serial.println("‚ö†Ô∏è JSON inv√°lido (estrutura incompleta). Mantendo arquivo antigo.");
//         }

//       } else {
//         Serial.println("‚ùå Erro ao abrir arquivo tempor√°rio para escrita.");
//       }
//     } else {
//       Serial.print("‚ö†Ô∏è Falha na requisi√ß√£o. C√≥digo HTTP: ");
//       Serial.println(httpCode);
//     }
    
//     https.end();

//   } else {
//     Serial.println("‚ùå Erro ao iniciar conex√£o HTTPS.");
//   }
// }

// void atualizarMotoristas() {
//   Serial.println("üîÑ Atualizando lista de motoristas...");
//   WiFiClientSecure client;
//   client.setInsecure();
//   HTTPClient https;

//   if (https.begin(client, apiMotoristas)) {
//     https.addHeader("User-Agent", "ESP8266");
//     https.addHeader("Accept", "application/json");
//     https.addHeader("Accept-Encoding", "identity");
//     https.setTimeout(10000);

//     int httpCode = https.GET();
//     if (httpCode == 200) {
//       Serial.println("üîÑ Baixando motoristas...");

//       File temp = SD.open("/temp_motoristas.json", FILE_WRITE);
//       if (temp) {
//         WiFiClient& stream = https.getStream();
//         unsigned long inicio = millis();
//         const unsigned long tempoLimite = 10000;
//         bool iniciouJson = false;

//         while ((millis() - inicio) < tempoLimite) {
//           if (stream.available()) {
//             char c = stream.read();
//             if (!iniciouJson) {
//               if (c == '[' || c == '{') {
//                 iniciouJson = true;
//                 temp.write(c);
//               }
//             } else {
//               temp.write(c);
//             }
//             inicio = millis();
//           }
//         }

//         temp.flush();
//         temp.close();
//         Serial.println("‚úÖ Motoristas tempor√°rios salvos.");

//         if (validarEstruturaJSON("/temp_motoristas.json")) {
//           Serial.println("üßæ JSON de motoristas v√°lido.");

//           if (SD.exists("/motoristas.json")) {
//             SD.remove("/motoristas.json");
//           }
//           SD.rename("/temp_motoristas.json", "/motoristas.json");
//           Serial.println("üì¶ motoristas.json atualizado com sucesso.");
//         } else {
//           Serial.println("‚ùå JSON de motoristas inv√°lido.");
//         }

//       } else {
//         Serial.println("‚ùå Erro ao abrir temp_motoristas.json.");
//       }
//     } else {
//       Serial.printf("Erro HTTP: %d\n", httpCode);
//     }

//     https.end();
//   } else {
//     Serial.println("‚ùå Erro ao iniciar conex√£o com motoristas.");
//   }
// }

void verificarMotoristaPorRFID() {
  File file = SD.open("/motoristas.json");
  if (!file) {
    Serial.println("‚ùå Falha ao abrir motoristas.json");
    motoristaEncontrado = false;
    return;
  }

  char c;
  do {
    c = file.read();
  } while (c != -1 && isspace(c));

  if (c != '[') {
    Serial.println("Formato inv√°lido em motoristas.json");
    file.close();
    motoristaEncontrado = false;
    return;
  }

  StaticJsonDocument<256> doc;

  while (file.available()) {
    DeserializationError err = deserializeJson(doc, file);
    if (err) {
      Serial.print("Erro ao ler motorista: ");
      Serial.println(err.c_str());
      break;
    }

    JsonObject motorista = doc.as<JsonObject>();
    const char* rfid = motorista["cartao_rfid"];

    if (rfidValor.equalsIgnoreCase(rfid)) {
      motoristaAtual.clear();
      motoristaAtual.set(motorista);
      motoristaEncontrado = true;
      Serial.println("‚úÖ Motorista encontrado:");
      Serial.print("  Nome: "); Serial.println(motorista["nome"].as<const char*>());
      break;
    }

    bool fim = false;
    while (file.available()) {
      char next = file.peek();
      if (next == ',') {
        file.read();
        break;
      } else if (isspace(next)) {
        file.read();
      } else if (next == ']') {
        file.read();
        fim = true;
        break;
      } else {
        break;
      }
    }

    if (fim) break;
    doc.clear();
  }

  file.close();

  if (!motoristaEncontrado) {
    Serial.println("‚ùå Nenhum motorista com esse RFID.");
  }
}

bool validarEstruturaJSON(const char* path) {
  File file = SD.open(path);
  if (!file) {
    Serial.println("‚ùå N√£o foi poss√≠vel abrir o arquivo para valida√ß√£o.");
    return false;
  }

  int chaves = 0;  // Conta { e }
  int colchetes = 0;  // Conta [ e ]

  while (file.available()) {
    char c = file.read();
    if (c == '{') chaves++;
    else if (c == '}') chaves--;
    else if (c == '[') colchetes++;
    else if (c == ']') colchetes--;

    // Se em algum momento as contagens ficarem negativas, j√° est√° errado
    if (chaves < 0 || colchetes < 0) {
      file.close();
      return false;
    }
  }

  file.close();

  return (chaves == 0 && colchetes == 0);  // JSON bem balanceado
}

// l√≥gicas para viajems ------------------------------------------------------------------------------------

// para salvar o ultimo limite no cart√£o sd
// para resolver o problema da demora de inicializa√ß√£o do gps
void salvarUltimoLimite() {
  delay(10);
  
  File file = SD.open("/ultimo_limite.txt", FILE_WRITE);
  if (file) {
    file.printf("%d,%d\n", vel_max, vel_max_chuva);
    file.flush();
    delay(10);
    file.close();
    Serial.printf("üíæ √öltimo limite salvo: %d / %d\n", vel_max, vel_max_chuva);
  } else {
    Serial.println("‚ùå Erro ao salvar √∫ltimo limite.");
  }
}

//caso o gps n√£o tenha sido iniciado, carrega o ultimo limite
bool carregarUltimoLimite() {
  delay(10);
  
  File file = SD.open("/ultimo_limite.txt");
  if (file) {
    String linha = file.readStringUntil('\n');
    file.close();
    int v1, v2;
    if (sscanf(linha.c_str(), "%d,%d", &v1, &v2) == 2) {
      vel_max = v1;
      vel_max_chuva = v2;
      Serial.printf("üìÇ Limite carregado do SD: %d / %d\n", vel_max, vel_max_chuva);
      return true;
    }
  }
  return false;
}

void verificarCercas(float lat, float lng) {
  File file = SD.open("/cercas.json");
  if (!file) {
    Serial.println("Erro ao abrir cercas.json");
    return;
  }

  // Verifica se o JSON come√ßa com [
  char c;
  do {
    c = file.read();
  } while (c != -1 && isspace(c)); // ignora espa√ßos

  if (c != '[') {
    Serial.println("Formato inv√°lido: JSON n√£o come√ßa com [");
    file.close();
    return;
  }

  StaticJsonDocument<1024> doc;
  bool encontrouAlguma = false;

  int menorVelMax = 999;         // Inicializa com valor alto
  int menorVelChuva = 999;
  const char* nomeMaisRestrito = nullptr;

  while (file.available()) {
    DeserializationError err = deserializeJson(doc, file);
    if (err) {
      Serial.print("Erro ao parsear objeto JSON: ");
      Serial.println(err.c_str());
      break;
    }

    JsonObject cerca = doc.as<JsonObject>();
    const char* nome = cerca["nome"];
    Serial.print("Verificando: ");
    Serial.println(nome);
    const char* velMaxStr = cerca["velocidade_max"];
    const char* velChuvaStr = cerca["velocidade_chuva"];
    JsonArray coords = cerca["coordenadas"];

    if (dentroDoPoligono(lat, lng, coords)) {
      int vel = atoi(velMaxStr);
      int velChuva = atoi(velChuvaStr);

      Serial.println("üõë Dentro de uma cerca:");
      Serial.print("üìç Nome: "); Serial.println(nome);
      Serial.print("üöó Limite: "); Serial.print(vel); Serial.println(" km/h");

      if (vel < menorVelMax) {
        menorVelMax = vel;
        menorVelChuva = velChuva;
        nomeMaisRestrito = nome;
        encontrouAlguma = true;
      }
    }

    bool terminou = false;
    while (file.available()) {
      char next = file.peek();
      if (next == ',') {
        file.read(); // consome v√≠rgula
        break;
      } else if (isspace(next)) {
        file.read(); // consome espa√ßo
      } else if (next == ']') {
        file.read(); // fim do array
        terminou = true;
        break;
      } else {
        break;
      }
    }

    if (terminou) break;

    doc.clear(); // limpa doc para o pr√≥ximo
  }

  file.close();

  if (encontrouAlguma) {
    vel_max = menorVelMax;
    vel_max_chuva = menorVelChuva;

    lcd.clear();
    // lcd.setCursor(0, 0);
    // lcd.print(String(nomeMaisRestrito).substring(0, 16));

    lcd.setCursor(0, 1);
    lcd.print("Limite: ");
    lcd.print(vel_max);
    lcd.print("km/h");

    //para definir se estpa chovendo futuramente usando o sensor
    chuva = false;
  } else {
    Serial.println("üì≠ Fora de qualquer cerca.");
    lcd.clear();
    lcd.setCursor(0, 1);
    lcd.print("Fora de qualquer cerca");
  }
}

bool dentroDoPoligono(float x, float y, JsonArray coords) {
  bool dentro = false;
  int n = coords.size();
  
  for (int i = 0, j = n - 1; i < n; j = i++) {
    float xi = atof(coords[i][0]), yi = atof(coords[i][1]);
    float xj = atof(coords[j][0]), yj = atof(coords[j][1]);
    
    if (((yi > y) != (yj > y)) && (x < (xj - xi) * (y - yi) / (yj - yi) + xi)) {
      dentro = !dentro;
    }
  }
  return dentro;
}

// Fun√ß√µes para a l√≥gica de registrar as posi√ß√µes --------------------------------------------------
String getTimestamp() {
  if (gps.date.isValid() && gps.time.isValid()) {
    char buffer[25];
    sprintf(buffer, "%04d-%02d-%02dT%02d:%02d:%02dZ",
            gps.date.year(),
            gps.date.month(),
            gps.date.day(),
            gps.time.hour(),
            gps.time.minute(),
            gps.time.second());
    return String(buffer);
  }
  return "0000-00-00T00:00:00Z";
}

unsigned long gerarIdUnico() {
    // Combina chip ID + timestamp + valor aleat√≥rio
    uint64_t chipId = ESP.getEfuseMac();
    uint32_t timestamp = millis();
    uint32_t randomVal = esp_random();
    
    unsigned long idUnico = ((chipId >> 32) ^ (chipId & 0xFFFFFFFF) ^ timestamp ^ randomVal);
    
    return idUnico;
}

//par recuperar viagens n√£o finalizadas
bool primeiroRegistro = true;

void recuperarViagemInterrompida() {
  // Verificar se a pasta /pendente existe
  if (!SD.exists("/pendente")) {
    Serial.println("‚úÖ Nenhuma viagem pendente");
    return;
  }

  File pendenteDir = SD.open("/pendente");
  if (!pendenteDir) {
    Serial.println("‚ùå Erro ao abrir pasta /pendente");
    return;
  }

  int viagensProcessadas = 0;
  
  // Processar todos os arquivos .json
  while (true) {
    File entry = pendenteDir.openNextFile();
    if (!entry) break;
    
    String nomeArquivo = String(entry.name());
    entry.close();

    // Pular diret√≥rios e arquivos que n√£o s√£o .json
    if (nomeArquivo.equals(".") || nomeArquivo.equals("..") || 
        nomeArquivo.indexOf(".") == -1 || !nomeArquivo.endsWith(".json")) {
      continue;
    }

    String caminhoCompleto = "/pendente/" + nomeArquivo;
    
    Serial.print("üîÑ Finalizando: ");
    Serial.println(nomeArquivo);

    // Adicionar "]}" para fechar o JSON
    File arquivo = SD.open(caminhoCompleto, FILE_APPEND);
    if (arquivo) {
      arquivo.print("]}");
      arquivo.flush();
      arquivo.close();
      
      // Mover para viagens
      String novoCaminho = "/viagens/" + nomeArquivo;
      if (SD.rename(caminhoCompleto, novoCaminho)) {
        Serial.print("‚úÖ Movido: ");
        Serial.println(novoCaminho);
        viagensProcessadas++;
      } else {
        Serial.println("‚ùå Falha ao mover arquivo");
      }
    } else {
      Serial.println("‚ùå Erro ao abrir arquivo");
    }
  }

  pendenteDir.close();

  if (viagensProcessadas > 0) {
    Serial.print("‚úÖ Processamento conclu√≠do. ");
    Serial.print(viagensProcessadas);
    Serial.println(" viagem(ns) recuperada(s)");
  } else {
    Serial.println("‚úÖ Nenhuma viagem pendente encontrada");
  }
}

void iniciarViagem() {
  viagemId = gerarIdUnico();
  nomeArquivoViagem = "/pendente/viagem_" + String(viagemId) + ".json";

  arquivoViagem = SD.open(nomeArquivoViagem, FILE_WRITE);
  if (!arquivoViagem) {
    Serial.println("‚ùå Erro ao criar arquivo de viagem");
    return;
  }

  snprintf(jsonBuffer, sizeof(jsonBuffer),
    "{"
    "\"viagem_id\":%lu,"
    "\"motorista_id\":%d,"
    "\"veiculo_id\":%d,"
    "\"registros\":[\n",
    viagemId,
    motoristaAtual["id"].as<int>(),
    VEICULO_ID
  );

  arquivoViagem.print(jsonBuffer);
  arquivoViagem.flush();

  viagemAtiva = true;
  primeiroRegistro = true;

  Serial.print("‚úÖ Viagem iniciada (Arquivo: ");
  Serial.print(nomeArquivoViagem);
  Serial.println(")");
}

void registrarPosicao(float lat, float lng, float vel, bool chuva) {
  if (!viagemAtiva) return;

  if (!primeiroRegistro) {
    arquivoViagem.print(",\n"); // adiciona v√≠rgula entre registros
  } else {
    primeiroRegistro = false;
  }

  snprintf(jsonBuffer, sizeof(jsonBuffer),
    "{\"timestamp\":\"%s\",\"lat\":%.6f,\"lng\":%.6f,"
    "\"vel\":%.2f,\"chuva\":%s,\"lim_seco\":%d,\"lim_chuva\":%d}",
    getTimestamp().c_str(),
    lat, lng, vel,
    chuva ? "true" : "false",
    vel_max, vel_max_chuva
  );

  arquivoViagem.print(jsonBuffer);
  arquivoViagem.flush();
  salvarUltimoLimite();

  Serial.print("Posi√ß√£o registrada, velocidade: ");
  Serial.println(vel);
}

void encerrarViagem() {
  if (!viagemAtiva || nomeArquivoViagem == "") {
    Serial.println("‚ùå Viagem n√£o est√° ativa para encerrar");
    return;
  }

  // Verificar se o arquivo ainda est√° aberto e v√°lido
  if (!arquivoViagem) {
    Serial.println("‚ùå Arquivo de viagem n√£o est√° aberto");
    viagemAtiva = false;
    return;
  }

  Serial.println("üîÑ Finalizando viagem...");

  // Registrar √∫ltima posi√ß√£o apenas se o GPS estiver ativo
  if (gps.location.isValid()) {
    float lat = gps.location.lat();
    float lng = gps.location.lng();
    float vel = gps.speed.isValid() ? gps.speed.kmph() : 0.0;
    
    if (!primeiroRegistro) {
      arquivoViagem.print(",\n");
    } else {
      primeiroRegistro = false;
    }

    snprintf(jsonBuffer, sizeof(jsonBuffer),
      "{\"timestamp\":\"%s\",\"lat\":%.6f,\"lng\":%.6f,"
      "\"vel\":%.2f,\"chuva\":false,\"lim_seco\":%d,\"lim_chuva\":%d}",
      getTimestamp().c_str(),
      lat, lng, vel,
      vel_max, vel_max_chuva
    );

    arquivoViagem.print(jsonBuffer);
    arquivoViagem.flush();
    Serial.println("‚úÖ √öltima posi√ß√£o registrada");
  }

  // Fechar o JSON
  arquivoViagem.print("]}");
  arquivoViagem.flush();
  arquivoViagem.close();
  Serial.println("‚úÖ JSON fechado");

  iniciarEnvioViagens();
  // Mover arquivo
  String novoNome = "/viagens/viagem_" + String(viagemId) + ".json";
  if (SD.rename(nomeArquivoViagem, novoNome)) {
    Serial.print("‚úÖ Viagem movida para: ");
    Serial.println(novoNome);
  } else {
    Serial.print("‚ùå Erro ao mover, mantendo em: ");
    Serial.println(nomeArquivoViagem);
  }

  // Resetar vari√°veis
  viagemAtiva = false;
  nomeArquivoViagem = "";
  viagemId = 0;
  primeiroRegistro = true;
  
  Serial.println("‚úÖ Viagem encerrada com sucesso");
}
