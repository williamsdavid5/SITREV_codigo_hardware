#include <Wire.h>
#include <LiquidCrystal_I2C.h>
#include <TinyGPSPlus.h>
#include <HardwareSerial.h>
#include <SPI.h>
#include <SD.h>
#include <FS.h>
#include <WiFi.h>
#include <HTTPClient.h>
#include <WiFiClientSecure.h>
#include <ArduinoJson.h>
#include <MFRC522.h>

// === Cart√£o SD ===
#define SD_CS 33
// SPIClass para SD no HSPI
SPIClass spiSD(HSPI);

bool limiteCarregadoOffline = false; //controle do limite carregado offline
//para controlar quando o gps nao funciona e precisamos de um limite offline
//ela evita que o sistema tente carregar repetidamente o limite do cart√£o
StaticJsonDocument<512> motoristaAtual;
bool motoristaEncontrado = false;

// === GPS ===
#define RX_GPS 16
#define TX_GPS -1
HardwareSerial gpsSerial(2);  // UART2
TinyGPSPlus gps;

bool gpsAtivo = false;

//coordenadas de origem
//necess√°rias no registro, mas o GPS demora para iniciar
//isso garante que elas n√£o fiquem vazias
// double origemLat = 0.0;
// double origemLng = 0.0;
// bool origemDefinida = false;

// === LCD ===
LiquidCrystal_I2C lcd(0x27, 16, 2);
boolean lcdFlag = false; //flag para controlar a impress√£o no lcd
//ajuda a impedir impress√£o constante desnecess√°ria

// === Wi-Fi e API ===
const char* ssid = "Duarte_Fotos";
const char* password = "05519558213";
const char* apiURL = "https://telemetria-fvv4.onrender.com/cercas";
const char* apiMotoristas = "https://telemetria-fvv4.onrender.com/motoristas/limpo";

// === Atualiza√ß√£o peri√≥dica ===
unsigned long ultimaAtualizacao = 0;
const unsigned long intervaloAtualizacao = 5 * 60 * 1000; // 5 minutos
unsigned long ultimaVerificacaoCercas = 0;
const unsigned long intervaloVerificacaoCercas = 5000; // 5 segundos

int vel_max;
int vel_max_chuva;

// === RFID ===
#define RST_PIN 27
#define SS_PIN_RFID 15

MFRC522 mfrc522(SS_PIN_RFID, RST_PIN);
SPIClass spiRFID(VSPI);  // VSPI para RFID

boolean rfidLido = false;
String rfidValor;
String ultimoUID = "";

#define LED_PIN 2  // LED embutido do ESP32
#define BUZZER_PIN 17

//variaveis auxiliares para a logica de registrar viajem offline
//para que os dados sejam armazenados no cart√£o
File arquivoViagem;
String nomeArquivoViagem = "";
bool viagemAtiva = false;
// bool primeiroRegistro = true;

// === ID do Ve√≠culo ===
const int VEICULO_ID = 2;
const String VEICULO_IDENTIFICADOR = "VEICULO_2";
const String VEICULO_MODELO = "Toyota Hilux";

//prototipo das fun√ß√µes
void verificarMotoristaPorRFID();
void salvarUltimoLimite();
void encerrarViagem(float destinoLat, float destinoLng);



unsigned long ultimaLeituraRFID = 0;
const unsigned long DEBOUNCE_RFID = 1000;
void processarCartao(String uid) {
  if (millis() - ultimaLeituraRFID < DEBOUNCE_RFID) {
    Serial.println("‚è∞ Debounce RFID: leitura ignorada (muito r√°pida)");
    return;
  }
  ultimaLeituraRFID = millis();

  // Guarda o UID atual para verifica√ß√£o
  String uidAnterior = rfidValor;
  rfidValor = uid;
  
  // Verifica se √© um motorista autorizado
  verificarMotoristaPorRFID();
  
  if (!motoristaEncontrado) {
    Serial.println("‚ùå Cart√£o n√£o autorizado");
    rfidValor = uidAnterior; // Restaura o UID anterior
    return;
  }

  if (!rfidLido) {
    // In√≠cio da viagem
    rfidLido = true;
    ultimoUID = uid;
    lcdFlag = true;
    Serial.println("üöó Viagem iniciada");
  } else {
    // Se j√° h√° viagem em andamento
    if (uid == ultimoUID) {
      // Mesmo motorista - encerra viagem
      rfidLido = false;
      limiteCarregadoOffline = false;
      salvarUltimoLimite();
      motoristaEncontrado = false;
      ultimoUID = "";
      lcd.clear();
      lcd.print("Viagem encerrada");
      Serial.println("üõë Viagem encerrada");

      encerrarViagem();

      delay(3000);
      lcd.clear();
      lcd.print("Inicie a viagem");
    } else {
      // Motorista diferente - encerra viagem atual e inicia nova
      Serial.println("üîÑ Motorista diferente - trocando viagem");
      
      // Primeiro encerra a viagem atual
      encerrarViagem();
      
      // Limpa vari√°veis da viagem anterior
      rfidLido = false;
      limiteCarregadoOffline = false;
      salvarUltimoLimite();
      ultimoUID = "";
      
      // Inicia nova viagem com o novo motorista
      rfidLido = true;
      ultimoUID = uid;
      lcdFlag = true;
      
      Serial.println("üöó Nova viagem iniciada com outro motorista");
      
      // Feedback visual
      lcd.clear();
      lcd.print("Troca motorista");
      lcd.setCursor(0, 1);
      lcd.print(motoristaAtual["nome"].as<const char*>());
      
      delay(2000);
    }
  }
}

//------------------------------------------------------------------------

void taskRFID(void* parameter) {
  Serial.println("RFID pronto");
  bool cartaoPresente = false;

  unsigned long ultimaLeitura = 0;
  static unsigned long ultimaVerificacao = millis();
  const unsigned long intervaloAntiRepeticao = 1000; // 1 segundo entre leituras

  for (;;) {
    if (mfrc522.PICC_IsNewCardPresent() && mfrc522.PICC_ReadCardSerial()) {
      String uid = "";
      for (byte i = 0; i < mfrc522.uid.size; i++) {
        if (mfrc522.uid.uidByte[i] < 0x10) uid += "0";
        uid += String(mfrc522.uid.uidByte[i], HEX);
      }
      uid.toUpperCase();

      // Preven√ß√£o contra leituras repetidas muito r√°pidas
      if (millis() - ultimaLeitura > intervaloAntiRepeticao) {
        ultimaLeitura = millis();

        if (!cartaoPresente) {
          // Apenas processa quando o cart√£o for inserido novamente
          digitalWrite(LED_PIN, HIGH);
          digitalWrite(BUZZER_PIN, HIGH);
          delay(50);
          digitalWrite(BUZZER_PIN, LOW);
          delay(35);
          digitalWrite(BUZZER_PIN, HIGH);
          delay(50);
          digitalWrite(BUZZER_PIN, LOW);

          Serial.print("üîç UID lido: ");
          Serial.println(uid);

          rfidValor = uid;
          processarCartao(uid);
          cartaoPresente = true;
        }
      }

      mfrc522.PICC_HaltA();
    } else {
      if (cartaoPresente) {
        digitalWrite(LED_PIN, LOW);
        cartaoPresente = false; // Liberar para pr√≥xima leitura
        // Pequeno delay ap√≥s remover o cart√£o para evitar leitura fantasma
        vTaskDelay(300 / portTICK_PERIOD_MS);
      }
    }

    if (millis() - ultimaLeitura > 10000) { // 10s sem resposta
      resetRFID();
      ultimaLeitura = millis();
    }

    // if (millis() - ultimaVerificacao > 10000) { // a cada 10s
    //   byte ver = mfrc522.PCD_ReadRegister(mfrc522.VersionReg);
    //   Serial.print("RFID Version check: ");
    //   Serial.println(ver, HEX);

    //   if (ver == 0x00 || ver == 0xFF) {
    //     Serial.println("‚ö†Ô∏è Vers√£o inv√°lida detectada, resetando RFID...");
    //     resetRFID();
    //   }
    //   ultimaVerificacao = millis();
    // }

    vTaskDelay(50 / portTICK_PERIOD_MS);
  }
}

void resetRFID() {
  Serial.println("‚ö†Ô∏è Resetando m√≥dulo RFID...");
  digitalWrite(RST_PIN, LOW);
  delay(100);
  digitalWrite(RST_PIN, HIGH);
  delay(100);
  SPI.end();
  SPI.begin(14, 12, 13, SS_PIN_RFID); 
  mfrc522.PCD_Init();  // Re-inicializa o RC522
  byte ver = mfrc522.PCD_ReadRegister(mfrc522.VersionReg);
  Serial.print("RFID Version ap√≥s reset: ");
  Serial.println(ver, HEX);
}

//------------------------------------------------------------------------

void setup() {
  Serial.begin(115200);

  pinMode(LED_PIN, OUTPUT);
  pinMode(BUZZER_PIN, OUTPUT);

  digitalWrite(LED_PIN, LOW);
  digitalWrite(BUZZER_PIN, LOW);

  limiteCarregadoOffline = false;

  pinMode(RST_PIN, OUTPUT);
  digitalWrite(RST_PIN, HIGH);
  delay(3000);

  gpsSerial.begin(9600, SERIAL_8N1, RX_GPS, TX_GPS);

  unsigned long inicioGPS = millis();
  bool gpsRespondendo = false;
  while (millis() - inicioGPS < 3000) { // 3 segundos de teste
      while (gpsSerial.available()) {
          char c = gpsSerial.read();
          Serial.write(c); // imprime cada caractere vindo do GPS
          gpsRespondendo = true;
      }
  }

  if (gpsRespondendo) {
      Serial.println("\n‚úÖ GPS respondendo!");
  } else {
      Serial.println("\n‚ö†Ô∏è GPS n√£o respondeu, verifique conex√µes.");
  }

  Wire.begin(4, 5);
  lcd.begin(16, 2);
  lcd.backlight();
  lcd.clear();
  lcd.print("Iniciando");
  lcd.setCursor(0, 1);
  lcd.print("Aguarde...");

  // Iniciar SPI do rfid
  digitalWrite(RST_PIN, LOW);
  delay(200);
  digitalWrite(RST_PIN, HIGH);
  delay(200);
  // delay(3000);

  SPI.begin(14, 12, 13, SS_PIN_RFID);
  // spiRFID.begin(14, 12, 13, SS_PIN_RFID);
  mfrc522.PCD_Init();
  delay(1000);
  Serial.print("RFID Version: ");
  byte v = mfrc522.PCD_ReadRegister(mfrc522.VersionReg);
  Serial.println(v, HEX);

  // Serial.println("Conectando ao Wi-Fi...");
  WiFi.begin(ssid, password);
  unsigned long inicioWifi = millis();
  // while (WiFi.status() != WL_CONNECTED && millis() - inicioWifi < 10000) {
  //   delay(500);
  //   Serial.print(".");
  // }

  // Serial.println(WiFi.status() == WL_CONNECTED ? "\nWi-Fi conectado!" : "\n‚ö†Ô∏è Wi-Fi n√£o dispon√≠vel. Usando dados locais.");

  // Inicia SD no HSPI (pinos 25,26,21,33)
  spiSD.begin(25, 26, 21, SD_CS);
  if (!SD.begin(SD_CS, spiSD)) {
    Serial.println("‚ùå Falha ao iniciar cart√£o SD");
    lcd.begin(16, 2);
    lcd.backlight();
    lcd.clear();
    lcd.print("Falha no SD!");
    while(true) delay(1000); // trava se falhar no SD
  }
  Serial.println("‚úÖ Cart√£o SD iniciado");

  if (!SD.exists("/viagens")) {
    SD.mkdir("/viagens");
    Serial.println("üìÅ Diret√≥rio /viagens criado");
  }

  // if (WiFi.status() == WL_CONNECTED) {
  //   atualizarCercas();
  //   atualizarMotoristas();
  // }

  xTaskCreatePinnedToCore(
    taskRFID,     // Fun√ß√£o da tarefa
    "RFID Reader",// Nome da tarefa
    4096,         // Tamanho da stack
    NULL,         // Par√¢metro
    1,            // Prioridade
    NULL,         // Handle da tarefa
    0             // Core 0 (o loop() roda no Core 1)
  );

  lcd.clear();
  lcd.setCursor(0, 0);
  lcd.print("Iniciando gps...");
}

//------------------------------------------------------------------------

void loop() {
  if (millis() - ultimaAtualizacao > intervaloAtualizacao) {
    if (WiFi.status() != WL_CONNECTED) {
      Serial.println("üîå Wi-Fi n√£o conectado. Tentando reconectar...");
      WiFi.begin(ssid, password);
      unsigned long inicio = millis();
      while (WiFi.status() != WL_CONNECTED && millis() - inicio < 10000) {
        delay(500);
        Serial.print(".");
      }
      Serial.println(WiFi.status() == WL_CONNECTED ? "\n‚úÖ Reconectado ao Wi-Fi!" : "\n‚ùå Falha ao reconectar.");
    }

    if (WiFi.status() == WL_CONNECTED) {
      atualizarCercas();
      atualizarMotoristas();
    }

    ultimaAtualizacao = millis();
  }

  if (rfidLido) {

    if (rfidLido && lcdFlag) {
      delay(50);
      lcd.clear();
      lcd.setCursor(0, 0);
      lcd.print("Bem vindo(a)");
      lcd.setCursor(0, 1);
      lcd.print(motoristaAtual["nome"].as<const char*>());

      iniciarViagem();
      delay(3000);

      lcdFlag = false;
    }

    if (!gpsAtivo && !limiteCarregadoOffline) {
      if (carregarUltimoLimite()) {
        lcd.clear();
        lcd.setCursor(0, 1);
        lcd.print("Limite:");
        lcd.setCursor(8, 1);
        lcd.print(vel_max);
        lcd.print("km/h");

        Serial.println("‚úÖ Limite carregado do SD na aus√™ncia do GPS.");
        limiteCarregadoOffline = true;
      } else {
        Serial.println("‚ö†Ô∏è Falha ao carregar limite do SD.");
      }
    }


  while (gpsSerial.available()) {
    gps.encode(gpsSerial.read());

    if (gps.location.isUpdated()) {
      gpsAtivo = true;

      float lat = gps.location.lat();
      float lng = gps.location.lng();
      float vel = gps.speed.kmph(); // velocidade do obd2
      bool chuva = false; // sensor IR

      // registra a primeira coordenada recebida do GPS como coordenada de origem
      // if (!origemDefinida && gps.location.isValid()) {
      //   origemLat = lat;
      //   origemLng = lng;
      //   origemDefinida = true;

      //   Serial.print("‚úÖ Origem registrada: ");
      //   Serial.print(origemLat, 6);
      //   Serial.print(", ");
      //   Serial.println(origemLng, 6);

      // }

      if (millis() - ultimaVerificacaoCercas > intervaloVerificacaoCercas) {
        Serial.print("Lat: "); Serial.println(lat, 6);
        Serial.print("Lng: "); Serial.println(lng, 6);
        ultimaVerificacaoCercas = millis();

        verificarCercas(lat, lng);
        registrarPosicao(lat, lng, vel, chuva);
      }
    }
  }
  } else {

    if (!lcdFlag) {
      lcd.clear();
      lcd.setCursor(0, 0);
      lcd.print("Inicie a Viagem");
      lcdFlag = true;
    }
  }
}

//------------------------------------------------------------------------

void atualizarCercas() {
  WiFiClientSecure client;
  client.setInsecure();
  HTTPClient https;

  if (https.begin(client, apiURL)) {
    https.addHeader("User-Agent", "ESP8266");
    https.addHeader("Accept", "application/json");
    https.addHeader("Accept-Encoding", "identity");
    https.setTimeout(10000); // 10 segundos de espera de resposta da API


    int httpCode = https.GET();
    if (httpCode == 200) {
      Serial.println("Resposta OK. Salvando no arquivo tempor√°rio...");

      // Salva em arquivo tempor√°rio
      File temp = SD.open("/temp_cercas.json", FILE_WRITE);
      if (temp) {
        WiFiClient& stream = https.getStream();

        unsigned long inicio = millis();
        const unsigned long tempoLimite = 10000; // 10 segundos
        bool iniciouJson = false;

        while ((millis() - inicio) < tempoLimite) {
          if (stream.available()) {
            char c = stream.read();

            if (!iniciouJson) {
              if (c == '[' || c == '{') {
                iniciouJson = true;
                temp.write(c);
              }
            } else {
              temp.write(c);
            }

            inicio = millis(); // Reinicia tempo sempre que l√™ algo
          }
        }

        temp.flush();
        temp.close();
        Serial.println("‚úÖ Arquivo tempor√°rio salvo com sucesso!");

        if (validarEstruturaJSON("/temp_cercas.json")) {
          Serial.println("‚úÖ JSON parece v√°lido! Substituindo arquivo oficial...");

          if (SD.exists("/cercas.json")) {
            SD.remove("/cercas.json");
          }

          SD.rename("/temp_cercas.json", "/cercas.json");
          Serial.println("üìù Substitui√ß√£o conclu√≠da.");
        } else {
          Serial.println("‚ö†Ô∏è JSON inv√°lido (estrutura incompleta). Mantendo arquivo antigo.");
        }

      } else {
        Serial.println("‚ùå Erro ao abrir arquivo tempor√°rio para escrita.");
      }
    } else {
      Serial.print("‚ö†Ô∏è Falha na requisi√ß√£o. C√≥digo HTTP: ");
      Serial.println(httpCode);
    }
    
    https.end();

  } else {
    Serial.println("‚ùå Erro ao iniciar conex√£o HTTPS.");
  }
}

void atualizarMotoristas() {
  WiFiClientSecure client;
  client.setInsecure();
  HTTPClient https;

  if (https.begin(client, apiMotoristas)) {
    https.addHeader("User-Agent", "ESP8266");
    https.addHeader("Accept", "application/json");
    https.addHeader("Accept-Encoding", "identity");
    https.setTimeout(10000);

    int httpCode = https.GET();
    if (httpCode == 200) {
      Serial.println("üîÑ Baixando motoristas...");

      File temp = SD.open("/temp_motoristas.json", FILE_WRITE);
      if (temp) {
        WiFiClient& stream = https.getStream();
        unsigned long inicio = millis();
        const unsigned long tempoLimite = 10000;
        bool iniciouJson = false;

        while ((millis() - inicio) < tempoLimite) {
          if (stream.available()) {
            char c = stream.read();
            if (!iniciouJson) {
              if (c == '[' || c == '{') {
                iniciouJson = true;
                temp.write(c);
              }
            } else {
              temp.write(c);
            }
            inicio = millis();
          }
        }

        temp.flush();
        temp.close();
        Serial.println("‚úÖ Motoristas tempor√°rios salvos.");

        if (validarEstruturaJSON("/temp_motoristas.json")) {
          Serial.println("üßæ JSON de motoristas v√°lido.");

          if (SD.exists("/motoristas.json")) {
            SD.remove("/motoristas.json");
          }
          SD.rename("/temp_motoristas.json", "/motoristas.json");
          Serial.println("üì¶ motoristas.json atualizado com sucesso.");
        } else {
          Serial.println("‚ùå JSON de motoristas inv√°lido.");
        }

      } else {
        Serial.println("‚ùå Erro ao abrir temp_motoristas.json.");
      }
    } else {
      Serial.printf("Erro HTTP: %d\n", httpCode);
    }

    https.end();
  } else {
    Serial.println("‚ùå Erro ao iniciar conex√£o com motoristas.");
  }
}

void verificarMotoristaPorRFID() {
  File file = SD.open("/motoristas.json");
  if (!file) {
    Serial.println("‚ùå Falha ao abrir motoristas.json");
    motoristaEncontrado = false;
    return;
  }

  char c;
  do {
    c = file.read();
  } while (c != -1 && isspace(c));

  if (c != '[') {
    Serial.println("Formato inv√°lido em motoristas.json");
    file.close();
    motoristaEncontrado = false;
    return;
  }

  StaticJsonDocument<512> doc;

  while (file.available()) {
    DeserializationError err = deserializeJson(doc, file);
    if (err) {
      Serial.print("Erro ao ler motorista: ");
      Serial.println(err.c_str());
      break;
    }

    JsonObject motorista = doc.as<JsonObject>();
    const char* rfid = motorista["cartao_rfid"];

    if (rfidValor.equalsIgnoreCase(rfid)) {
      motoristaAtual.clear();
      motoristaAtual.set(motorista);
      motoristaEncontrado = true;
      Serial.println("‚úÖ Motorista encontrado:");
      Serial.print("  Nome: "); Serial.println(motorista["nome"].as<const char*>());
      break;
    }

    bool fim = false;
    while (file.available()) {
      char next = file.peek();
      if (next == ',') {
        file.read();
        break;
      } else if (isspace(next)) {
        file.read();
      } else if (next == ']') {
        file.read();
        fim = true;
        break;
      } else {
        break;
      }
    }

    if (fim) break;
    doc.clear();
  }

  file.close();

  if (!motoristaEncontrado) {
    Serial.println("‚ùå Nenhum motorista com esse RFID.");
  }
}

bool validarEstruturaJSON(const char* path) {
  File file = SD.open(path);
  if (!file) {
    Serial.println("‚ùå N√£o foi poss√≠vel abrir o arquivo para valida√ß√£o.");
    return false;
  }

  int chaves = 0;  // Conta { e }
  int colchetes = 0;  // Conta [ e ]

  while (file.available()) {
    char c = file.read();
    if (c == '{') chaves++;
    else if (c == '}') chaves--;
    else if (c == '[') colchetes++;
    else if (c == ']') colchetes--;

    // Se em algum momento as contagens ficarem negativas, j√° est√° errado
    if (chaves < 0 || colchetes < 0) {
      file.close();
      return false;
    }
  }

  file.close();

  return (chaves == 0 && colchetes == 0);  // JSON bem balanceado
}

// para salvar o ultimo limite no cart√£o sd
// para resolver o problema da demora de inicializa√ß√£o do gps
void salvarUltimoLimite() {
  delay(10);
  
  File file = SD.open("/ultimo_limite.txt", FILE_WRITE);
  if (file) {
    file.printf("%d,%d\n", vel_max, vel_max_chuva);
    file.flush();
    delay(10);
    file.close();
    Serial.printf("üíæ √öltimo limite salvo: %d / %d\n", vel_max, vel_max_chuva);
  } else {
    Serial.println("‚ùå Erro ao salvar √∫ltimo limite.");
  }
}

//caso o gps n√£o tenha sido iniciado, carrega o ultimo limite
bool carregarUltimoLimite() {
  delay(10);
  
  File file = SD.open("/ultimo_limite.txt");
  if (file) {
    String linha = file.readStringUntil('\n');
    file.close();
    int v1, v2;
    if (sscanf(linha.c_str(), "%d,%d", &v1, &v2) == 2) {
      vel_max = v1;
      vel_max_chuva = v2;
      Serial.printf("üìÇ Limite carregado do SD: %d / %d\n", vel_max, vel_max_chuva);
      return true;
    }
  }
  return false;
}

void verificarCercas(float lat, float lng) {
  File file = SD.open("/cercas.json");
  if (!file) {
    Serial.println("Erro ao abrir cercas.json");
    return;
  }

  // Verifica se o JSON come√ßa com [
  char c;
  do {
    c = file.read();
  } while (c != -1 && isspace(c)); // ignora espa√ßos

  if (c != '[') {
    Serial.println("Formato inv√°lido: JSON n√£o come√ßa com [");
    file.close();
    return;
  }

  StaticJsonDocument<2048> doc;
  bool encontrouAlguma = false;

  int menorVelMax = 999;         // Inicializa com valor alto
  int menorVelChuva = 999;
  const char* nomeMaisRestrito = nullptr;

  while (file.available()) {
    DeserializationError err = deserializeJson(doc, file);
    if (err) {
      Serial.print("Erro ao parsear objeto JSON: ");
      Serial.println(err.c_str());
      break;
    }

    JsonObject cerca = doc.as<JsonObject>();
    const char* nome = cerca["nome"];
    Serial.print("Verificando: ");
    Serial.println(nome);
    const char* velMaxStr = cerca["velocidade_max"];
    const char* velChuvaStr = cerca["velocidade_chuva"];
    JsonArray coords = cerca["coordenadas"];

    if (dentroDoPoligono(lat, lng, coords)) {
      int vel = atoi(velMaxStr);
      int velChuva = atoi(velChuvaStr);

      Serial.println("üõë Dentro de uma cerca:");
      Serial.print("üìç Nome: "); Serial.println(nome);
      Serial.print("üöó Limite: "); Serial.print(vel); Serial.println(" km/h");

      if (vel < menorVelMax) {
        menorVelMax = vel;
        menorVelChuva = velChuva;
        nomeMaisRestrito = nome;
        encontrouAlguma = true;
      }
    }

    bool terminou = false;
    while (file.available()) {
      char next = file.peek();
      if (next == ',') {
        file.read(); // consome v√≠rgula
        break;
      } else if (isspace(next)) {
        file.read(); // consome espa√ßo
      } else if (next == ']') {
        file.read(); // fim do array
        terminou = true;
        break;
      } else {
        break;
      }
    }

    if (terminou) break;

    doc.clear(); // limpa doc para o pr√≥ximo
  }

  file.close();

  if (encontrouAlguma) {
    vel_max = menorVelMax;
    vel_max_chuva = menorVelChuva;

    lcd.clear();
    lcd.setCursor(0, 0);
    lcd.print(String(nomeMaisRestrito).substring(0, 16));

    lcd.setCursor(0, 1);
    lcd.print("Limite: ");
    lcd.print(vel_max);
    lcd.print("km/h");
  } else {
    Serial.println("üì≠ Fora de qualquer cerca.");
    lcd.clear();
    lcd.setCursor(0, 0);
    lcd.print("Fora de qualquer cerca");
  }
}

bool dentroDoPoligono(float x, float y, JsonArray coords) {
  int i, j, n = coords.size();
  bool dentro = false;

  for (i = 0, j = n - 1; i < n; j = i++) {
    float xi = atof(coords[i][0]), yi = atof(coords[i][1]);
    float xj = atof(coords[j][0]), yj = atof(coords[j][1]);

    if (((yi > y) != (yj > y)) &&
        (x < (xj - xi) * (y - yi) / (yj - yi + 1e-9) + xi)) {
      dentro = !dentro;
    }
  }

  return dentro;
}

// Fun√ß√µes para a l√≥gica de registrar as posi√ß√µes --------------------------------------------------
String getTimestamp() {
  if (gps.date.isValid() && gps.time.isValid()) {
    char buffer[25];
    sprintf(buffer, "%04d-%02d-%02dT%02d:%02d:%02dZ",
            gps.date.year(),
            gps.date.month(),
            gps.date.day(),
            gps.time.hour(),
            gps.time.minute(),
            gps.time.second());
    return String(buffer);
  }
  return "0000-00-00T00:00:00Z";
}

void iniciarViagem() {
  delay(50);
  
  nomeArquivoViagem = "/viagens/viagem_" + String(millis()) + ".json";
  arquivoViagem = SD.open(nomeArquivoViagem, FILE_WRITE);

  if (!arquivoViagem) {
    Serial.println("‚ùå Erro ao criar arquivo de viagem");
    return;
  }
  
  // Cabe√ßalho simplificado - primeira linha
  arquivoViagem.print("{\"motorista_id\":");
  arquivoViagem.print(motoristaAtual["id"].as<int>());
  arquivoViagem.print(",\"veiculo_id\":");
  arquivoViagem.print(VEICULO_ID);
  arquivoViagem.print(",\"inicio\":\"");
  arquivoViagem.print(getTimestamp());
  arquivoViagem.print("\"}");
  arquivoViagem.println(); // Nova linha para o pr√≥ximo registro
  
  arquivoViagem.flush();
  delay(10);
  
  viagemAtiva = true;
  Serial.println("‚úÖ Cabe√ßalho da viagem criado");
}

void registrarPosicao(float lat, float lng, float vel, bool chuva) {
  if (!viagemAtiva) return;
  
  delay(10);
  
  arquivoViagem.print("{\"timestamp\":\"");
  arquivoViagem.print(getTimestamp());
  arquivoViagem.print("\",\"lat\":");
  arquivoViagem.print(lat, 6);
  arquivoViagem.print(",\"lng\":");
  arquivoViagem.print(lng, 6);
  arquivoViagem.print(",\"vel\":");
  arquivoViagem.print(vel, 2);
  arquivoViagem.print(",\"chuva\":");
  arquivoViagem.print(chuva ? "true" : "false");
  arquivoViagem.print(",\"lim_seco\":");
  arquivoViagem.print(vel_max);
  arquivoViagem.print(",\"lim_chuva\":");
  arquivoViagem.print(vel_max_chuva);
  arquivoViagem.print("}");
  arquivoViagem.println(); // Nova linha para o pr√≥ximo registro
  
  arquivoViagem.flush();
  salvarUltimoLimite();
  delay(10);
}

void encerrarViagem() {
  if (!viagemAtiva) return;

  delay(20);
  
  // √öltima linha com coordenadas finais
  arquivoViagem.print("{\"fim\":\"");
  arquivoViagem.print(getTimestamp());
  arquivoViagem.print("\",\"dest_lat\":");
  arquivoViagem.print(gps.location.isValid() ? gps.location.lat() : 0.0, 6);
  arquivoViagem.print(",\"dest_lng\":");
  arquivoViagem.print(gps.location.isValid() ? gps.location.lng() : 0.0, 6);
  arquivoViagem.print("}");
  arquivoViagem.println();
  
  arquivoViagem.flush();
  delay(20);
  
  arquivoViagem.close();
  delay(10);
  
  viagemAtiva = false;
  // origemDefinida = false;
  Serial.println("‚úÖ Viagem encerrada");
}



